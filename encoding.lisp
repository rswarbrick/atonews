(in-package :atonews)

;; (Literal representation) Octets with decimal values of
;; 33 through 60 inclusive, and 62 through 126, inclusive,
;; MAY be represented as the US-ASCII characters which
;; correspond to those octets (EXCLAMATION POINT through
;; LESS THAN, and GREATER THAN through TILDE,
;; respectively).
(defun quoted-printable-self-representable? (c)
  (or (<= 33 (char-code c) 60)
      (<= 62 (char-code c) 126)))

(defun ascii-horizontal-whitespace? (c)
  "Return T if C is a space or a tab."
  (member c '(#\Space #\Tab)))

(defun consume-notrans (chars-left str)
  "Returns APPENDME, FULL?, UNUSED"
  (let ((length (length str)))
    (cond
      ((>= chars-left length)
       (values str (= chars-left length) nil))
      (t
       (values (subseq str 0 chars-left) t (subseq str chars-left))))))

(defun encode-char (ch)
  "Returns a string that encodes the given char"
  (if (quoted-printable-self-representable? ch)
      (string ch)
      (format nil "浸铂О佝ㄣ镥蜚ㄢ徕屐后趄轭绛麸镢翦趔篝蜷铉汨┅ъ轶舂┅ㄤ彐躅泔铙蹴瀛趄犷ㄣ栳蝮戾骠篝镳糸镱犰灬篝咯⒁弭躜铙列信文团普烫楷瘴沼拍ㄤ邈灬蝈ㄩ珙矧灬篝咯戾è孱泔溴礤铒ㄥ鲥蝙＇聃雉邃痱轭翎忪瀛箦戽蝈痱弩孱翎忪蹇篝颟┅ㄩ铒孱泔溴礤咯ㄣ镱篚礤铒趄犷汨狎蟓戾骠篝颟戾è徙悌ㄦ轵篝躅躞邃┅ㄤ邈汨狎蟓戾骠辈ㄤ雉轫弩戾铉翳篝颟戾è蝈ㄥ钽镤瀛汨狎ㄡ蝈篝瞟┅麒孱戾铉翳蝈皓汨狎蟓戾骠箦翩骈蝮舡躅躞邃瞟蝈趱蝾┅ㄤ邈汨狎蟓戾骠戾铉翳蝈皓瘐箬蝈徙悌┅ㄩㄡ钿骈蝮舡躅躞邃骈蝮舡躅躞邃┅鲠祯弩铋篝颟鲠祯弩ㄦ矧磲铋⒔旷翩缚芽窿拷铗弼弪箦徙悌矧ㄡ钿骈蝮舡躅躞邃舂汨狎蟓戾骠┅ㄡ钿骈蝮舡躅躞邃篚怏羼篝骈蝮舡躅躞邃┅┅┅┅ㄤ彐躅泔铙蹴瀛箴徙ㄣ栳蝮戾骠篝灬篝咯⒁弭躜铙列信文团普烫楷瘴沼拍沈蜷翎糸铉禊眭篝瞌戾狯趄衢扉铉麒轸弩疳沐镱扉铄箫殒蝈犰轶黠蹯滹翳狒铄邃麸孱泔溴翳篝蹑娈燥孱篚蝈翳狒滹瞌孱翳扉铄铄邃塘釉麸忮驷祗瀹ㄩ铒灬篝咯ㄣ镱篚礤铒趄犷汨狎蟓戾骠篝颟ㄣ镱篚礤趄犷汨狎蟓戾骠篝颟┅ㄤ彐躅孱泔溴舶捶桢徜弪篝汨狎蟓戾骠⒁弭躜篝蜷铉孱泔溟铉釉篚汨翳狒遽汨扉铄轶狒盹篝范汨狎徙翦蝮祜铉轭沆蹁轭翳娇拷忾趔犷篚汨翳狒翳骈蝮痖邈轶狒盹篝萌烈迎膛圃祜铉眭祠轲戾鲠祯瀛忾钿ㄢ祜汶箴徙蹇痱邃殂狒瀛忪镢塍＇狍汩榄栾蜷镱翎飙麒轸弩疳沐篝颟戾è扉铄螬ㄡ沣┅ㄦ戾è翎脲篝颟箦翩徙ㄣ镱汜翦钺翦篝蜷铉徙篝颟ㄤ邈汨狎蟓戾骠戾铉翳篝颟┅篝矧ī瘐箬徙扉铄螬箦翩徙箦翩汨狎蟓戾骠返┅ㄤè忪镢塍戾骠忪镢塍ㄣ潋忪镢塍戾骠┅è铛祆忪镢塍戾骠┅戾è灬篝铛祆ㄣ潋忪镢塍戾骠┅翳轶忪镢ㄣ狎忪镢塍戾骠┅祜镳眭祠轲戾鲠祯瀛忾钿ㄡ痧孱漤骢祆躅躞邃ㄦ躅汜祆ㄩ箴徙蹇＇泔铙蹴瀛箴徙＇泔铙蹴瀛趄犷螬ū汨狎蟓戾骠翳轶忪镢灬篝咯麒孱狃疱钿礤翎脲狃疱钿礤┅麒孱骢祆篝矧濠ㄩ躅躞邃箦翩翳轶忪镢躅躞邃蝈趱蝾┅┅箦翩箴徙蹇铒箴徙蹇┅ㄦ矧磲铋两A" (nreverse lines) acc)))))
