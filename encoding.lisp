(in-package :atonews)

(defvar *rfc2047-header-encoding-alist*
  '(("^Newsgroups" . nil)
    ("^Followup-To" . nil)
    ("^Message-ID" . nil)
    ("(Resent-)?(From|Cc|To|Bcc|(In-)?Reply-To|Sender|\
Mail-Followup-To|Mail-Copies-To|Approved)"
     . address-mime)
    (t . mime))
  "From Gnus. Use nil for no encoding needed; MIME for ordinary escaping and
ADDRESS-MIME for addresses.")

(defun encoding-method (header-name)
  "Return the method to use (nil, mime or address-mime) to encode the given
header."
  (cdr (find-if
        (lambda (first) (or (eq first t) (cl-ppcre:scan first header-name)))
        *rfc2047-header-encoding-alist*
        :key #'car)))

(defun skip-chars (seq index skip-over &key negate? backwards)
  "Skips forwards over elements of SEQ in SKIP-OVER, starting at INDEX. If
NEGATE?, then skips elements not in SKIP-OVER. If BACKWARDS is true, instead run
backwards starting at index and end at the last character (backwards) in
SKIP-OVER that we see."
  (let* ((func (if negate? #'position-if #'position-if-not))
         (keyargs (list :from-end backwards
                        (if backwards :end :start) index))
         (pos (apply func (lambda (x) (find x skip-over)) seq
                     keyargs)))
    (if backwards (1+ (or pos -1)) (or pos (length seq)))))

(defun skip-forward-whitespace (str index &optional negate)
  "Skips forwards over whitespace in STR, starting at INDEX. If NEGATE, then
skips non-whitespace."
  (skip-chars
   str index (coerce #(#\Space #\Linefeed #\Tab) 'string) :negate? negate))

(defun skip-forward-word (str index)
  "Skip forwards over whitespace, then non-whitespace in STR starting at
INDEX (ie one word)."
  (let ((end-space (skip-forward-whitespace str index)))
    (skip-forward-whitespace str end-space t)))

(defun rfc2047-needs-encoding? (str)
  "Return T if we need to do something non-trivial to encode the given string."
  (or (not (every (lambda (c) (<= (char-code c) 126)) str))
      (and (search "=?" str) (search "?=" str))))

(defun quoted-printable-self-representable? (c)
  (or (<= 33 (char-code c) 60)
      (<= 62 (char-code c) 126)))

(defun skip-forward-words-needing-encoding (str index)
  "Skips forwards over elements of STR that need encoding, starting at
index. Returns the position at the end of the last successive word that needs
encoding. This will either be at whitespace or at the end of STR."
  (let ((pos index) (end index))
    (loop
       (when (= pos (length str)) (return))
       (setf end (skip-forward-word str pos))
       (unless (rfc2047-needs-encoding? (subseq str pos end)) (return))
       (setf pos end))
    pos))

(defun rfc2047-encode-char (ch)
  "Returns a string that encodes the given char"
  (if (quoted-printable-self-representable? ch)
      (string ch)
      (format nil "浸铂О佝ㄣ镥蜚ㄢ徕屐后趄轭绛麸镢翦趔篝蜷铉汨┅ъ轶舂┅ㄤ彐躅蜴悴按翻孱泔溴篝篝狎孱镳糸镱犰磲聃雉戾铉翳返┅⑴钽镤翳篚怏羼蹂钽狍聃雉邃痱轭翎忪狍疱蜴悴按樊戾è聃雉螬ㄡ沣戾铉翳癌ㄦ戾è篝矧ī瘐箬ㄦ矧磲铋⒔旷翩缚芽窿拷铗弼弪箦徙悌聃雉螬箦翩戾铉翳癌┅ㄤè篝狎ū瞟┅è孱洎戾è孱泔溴蜴悴按翻孱泔溴汨狎ㄡ蝈篝瞟┅麒孱ǐ辈戾铉翳孱泔溴洎戾铉翳磲聃雉戾铉翳篝矧濠瘐箬孱泔溴徙悌ㄩ钽戾铉翳戾铉翳孱泔溴洎┅篝矧濠蝈漉沐灬礅溽ㄡ猢ㄣ镱汜翦钺翦篝蜷铉猢铗弼弪箦聃雉螬┅┅ㄤ彐躅蜴悴按翻孱泔溴桢徜弪脲鲠祯濠⑴钽镤翳玳鲥桢徜弪泔蝌邈綮骘移舶捶义趱蝾翳篝蜷铉骘翳孱泔溴鲠祯瀹换箩箦镱翳箫躜沐镦蜴悴按樊屐骝镯穷躞戾è礤翳镤ㄥ钽镤轭绛礤翳镤脲┅篝狎癌痫癌ㄣ镱è铒ㄡ钿礤翳镤蜴悴按翻铄邃蟓孱泔溟铉鲠祯濠┅换砰翳弪铒礤翳镤躞邃矧铒孱泔溟铉蝈聃轵邃鲠祯濠è羼礤翳镤ы轫濠换澡轶轶翳㈠狍汜箦祜镳换与轲骘蝼狎秭弪犷麒轸弩疳沐箦翩篝狎箅轲骘蝼狎洵麒轸弩疳沐鲠祯篝狎舂麒孱篝狎戾铉翳鲠祯濠蝈趱蝾┅换揍祀秭弪黠蜾翳狒铄邃孱泔溟铉犷沆蹴翳屙麸珏翳弪麸滹轭换镱忉翥璁箦翩痫箅轲骘蝼狎洵黠蜾蟓铄邃轭绛孱泔溟铉鲠祯篝狎舂ㄩ痫篝狎舂戾è孱泔溴洵箦泗轱蜴悴按翻孱泔溴鲠祯篝狎痫螬┅箦翩鲠祯ㄣ镱汜翦钺翦篝蜷铉篚怏羼鲠祯篝狎舂孱泔溴洵箦泗轱篚怏羼鲠祯痫螬篝狎ǐ篝狎戾铉翳孱泔溴洵箦泗轱瞟┅换澡铄黠蜾轶痱弩蹴徕禊骈铄箫箅轲秭弪轸箦翩篝狎箅轲骘蝼狎洵黠蜾鲠祯痫螬┅鲠祯濠è羼礤翳镤п滗蝈篌黹礤换澡轶轶磲篌轹屐盹蝈泔眇扉汜翦洮轭翳屣蝙语骚顼轭麸瘐铘镱换翳溟骀殂蹯豉骚祆翳蝻犷弪蝻殒箬秕熹栳鲥滹铄换箫礤翳轭绠澡孱殒篝狎珏趑轭弪蝻蝮骚祆黩轸翳泔溴涵湘换泔躜箦汨邈脲遽蜢殄骘铒铄邃轭孱泔溟铉箫骚祆牾篝翳蝻换犷弪蝻桢蝈ㄥ蝌矧⒘哪遗佑蜕团孱泔溟铉铒弭痫螋邃┅ㄥ蝌矧⒄铍铒黝孱泔溟铉礤翳镤立礤翳镤┅┅ㄤ彐躅蜴悴按翻骘熹篝镳糸镱犰ㄢ彗轭铋铉镦扉铄癌⑵镬釉狍疱蜴悴按樊歪脲屡巧挝晌黔掀躺闻铄玑糸鲥殒翳弪麽桢徜弪遽蜢殄虍零趔溴篝蝓泗轹屐镱釉耶换酗螋邃箦黹轭翦祆殓孱綮骝镯穷躞戾è秕舂ㄦ轵篝舂痫癌ㄢ镬忮玳铑轭绛镦扉铄黠蜾篝狎铋飑聍矧洵篝狎铋飑灬忮祗è狒孱淇ī痫戾铉翳篝颟┅ㄡ舡翳轶ㄥㄡ蝈篝痫螬┅祜镫轭绛狒蝈珏ㄣ飙痧泸搴筱犷ㄡ痧孱Ж后羼蹂钽后翎螋犷汨矧扉篝ㄣ飙痧泸搴疳蝮瀛篝蜷铉蝈珏┅篝后翎螋痫螬箅轲秭弪ㄣ栳蝮镳糸镱犰铄玑翦咯箦翩痫箅轲汨狎篝痫汨狎侯彗狒蹇铄玑翦咯┅箅轲铒铟栝翦ī箅轲秭弪ㄣ镥蜚Ж＼羽徙＼葬＼五黛轭＼义趱蝾篝蜷铉舂痱镢弩蟓怛遽ī麒孱ㄡ钿矧黠蜾篝狎聍矧洵篝狎舂ō痫怙飑范┅箦翩痫箅轲汨狎篝矧黠蜾篝狎聍矧洵篝狎舂Ж＼羽徙＼葬猢衡徙膑狎潴舂黠蜾篝狎铋聍矧洵篝狎铋飑躅戾篌痫怙飑ㄥ蝌矧⒚秕熹铒骈钿篚轸徕戾怛遽痫轭舢┅瘐箬篚怏羼篝磲怙飑痫螬秕舂换澡轶轶翳忾麒弪麇徙溴篝蝓泗轹屐镱釉耶涉翳换铄汨狎徙翦轶瞌箴徙瀣麇铄邃镱瀣箫蝈痨徙翳换痱弼轱躞汨狎徙翦怡镱瀹躅戾篌祜镫轭绛狒③荇茛箦翩ㄡ蝈篝ū痫螬＼羽徙濠ㄤ邈痫螬箦翩怙痫螬换族滹瞌麽铘麸怛遽徵衢忮骘蝈翳骈蝮麒轸弩疳沐箫换牾眇骘蝼狎洚箅轲秭弪Ж＼羽徙＼葬猢躅戾篌ㄡ舡孱淇ㄩ钽痫螬┅┅祜镳麒孱ㄡ舡孱淇蝈趱蝾┅换涉麇铄邃邃麸怛遽翳篝蜷铉铒轶翳糸礤麸滹轸痱镢弩蟓怛遽氅ㄣ镱è狒翳轶＼五黛轭濠ㄩ钽痫螬箦翩怙痫黠蜾篝狎铋聍矧洵篝狎铋痫箅轲骘蝼狎洵麒轸弩疳沐篝痫螬换裴狒戾狍镱铒瞽箴徙汨狎躅戾篌矧ㄡ舡孱淇ㄡ舡翳轶＼五黛轭濠ㄩ钽痫螬┅è狒翳轶＼义趱蝾ㄩ钽痫螬è礤礅弪ㄡ蝈篝痫螬Ж＼羽徙＼葬猢箅轲秭弪Ж＼羽徙＼葬猢躅戾篌骈蝮箦翩黠蜾篝狎痫螬┅è铒黠蜾篝狎舂换留翳盹礤铘麇滹瞌栳鲥犷铄痨徙翳狒麇汜怛遽氍箫换箅轲骘蝼狎躅糸麇骈钿箫礤麒弪麸滹箫湘泔躜箦麇т换忮趑弪忮汜蝈骢麸箅轲聍矧潴泔蝌邈綮ㄩ祜镫轭绛狒⒔苘扣藿茛痱镧躅戾篌骈蝮箦翩聍矧洵篝狎痫螬箅轲秭弪Ж＼羽徙＼葬＼五黛轭＼义趱蝾＼僵舂ㄩㄡ舡翳轶＼僵ㄩ钽痫螬箅轲铒铟栝翦┅┅箅轲铒铟栝翦┅箦翩骈蝮铋飑痱镢弩蟓怛遽氅铗弼弪箦ㄣ镱篚怏羼篝磲怙癌秕舂┅ㄤ彐躅蜴悴按翻骘蝽狒桢徜弪钺礤鲠祯濠⒁弭躜箦钿徕戾鲥蝮轱镦翳玳鲥桢徜弪溽翎ㄦ矧磲铋梁窿撖"
          name 
          (rfc2047-fold (rfc2047-encode-header name value)
                        (- (+ 2 (length name))))))
